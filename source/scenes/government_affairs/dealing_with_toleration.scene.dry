title: Dealing with Toleration
new-page: true
is-card: true
card-image: img/portraits/BrüningHeinrich.jpg
view-if: dealing_with_toleration_timer = 0 and dnvp_toleration and chancellor_party == "Z"
on-arrival: dealing_with_toleration_timer += 6; month_actions += 1
tags: govt_affairs
#subtitle: Despite not being in government, we can still provide support.

= Dealing with Toleration

We are currently acting as external support to the government of Chancellor [+ chancellor +]. How should we use our position?

- @break_toleration
- @urge_against_austerity
- @improve_relations
- @new_grand_coalition
- @stay_course: Stay the course, for stability's sake.

@break_toleration
view-if: not constructive_vonc
title: End the toleration agreement with a vote of no confidence!

Are we sure about this? A vote of no confidence would trigger new elections, and potentially an increased vote-share for the extremist parties.

- @vonc: Yes, end this arrangement!
- @stay_course: No, we should stay the course.

@vonc
on-arrival: spd_in_government = 0; dnvp_toleration = 0; in_grand_coalition = 0; chancellor = "Brüning"; chancellor_party = "Z"; next_election_time = time + 3 if next_election_time > time+3; next_election_month = month + 3 if next_election_time >= time+3; next_election_year = year if next_election_time >= time+3; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12; dnvp_toleration = 0; right_strength += 8; anti_democracy -= 5; new_middle_uspd -= 3; rural_uspd -= 3; old_middle_uspd -= 3; catholics_uspd -= 3; christiansocial_dissent += 5; right_dissent -= 5; uspd_relation -= 4; z_relation -= 4; wp_relation -= 4; lvp_relation -= 4

We have called for a vote of no confidence, and have brought down this government. Chancellor [+ chancellor +] currently rules as acting chancellor.

New elections have been set for [+ next_election_month : month +] [+ next_election_year +].

@urge_against_austerity
title: Urge the coalition against austerity politics, and to increase social spending.
choose-if: budget >= 1
unavailable-subtitle: This can only happen with a budget surplus.
go-to: urge_success if z_relation >= 40 and ((wp_relation >= 40 and not lvp_formed) or (lvp_relation >= 40 and lvp_formed)); urge_failure if z_relation < 40 or ((wp_relation < 40 and not lvp_formed) or (lvp_relation < 40 and lvp_formed))

@urge_success
on-arrival: welfare += 1; budget -= 1; workers_uspd += 5*(1-dissent); unemployed_uspd += 5*(1-dissent); z_relation -= 3; uspd_relation -= 3; wp_relation -= 3; lvp_relation -= 3; christiansocial_dissent -= 5; agrarian_dissent -= 5; youngconservative_dissent -= 5; dvp_right += 1; ddp_right += 0.5; lvp_right += 1

We have successfully urged the coalition parties to increase social spending! Unfortunately, the center-right parties increasingly see us as a nuisance, and might not wish to continue these negotiations.

@urge_failure
on-arrival: z_relation -= 3; uspd_relation -= 3; wp_relation -= 3; lvp_relation -= 3

Unfortunately, our negotiations have met with failure. The center-right parties increasingly see us as a nuisance, and might not wish to continue these negotiations.


@improve_relations
title: Improve relations with the parties of the coalition.
on-arrival: z_relation += 5*(1-dissent); uspd_relation += 4*(1-dissent); wp_relation += 4*(1-dissent); lvp_relation += 4*(1-dissent); right_dissent += 5; dvp_left += 1; ddp_left += 0.5; ddp_cohesion -= 0.5; lvp_left += 1

We have increased our level of contact and discussions with the parties of the coalition, primarily the <span style="color: #000000;">Center Party</span> and [? if not lvp_formed:<span style="color: #C0A054;">**DVP**</span>?][? if lvp_formed:<span style="color: #FFCC00;">**LVP**</span>?].

Our attempts to reach out to the center-right are met with disdain by leftists within our party.




@new_grand_coalition
title: Can we join the government in a new grand coalition?
view-if: neo_weimar_coalition >= 50
choose-if: ((z_relation >= 50 and dnvp_ideology == "Radical") or (z_relation >= 60 and dnvp_ideology == "Moderate")) and (((uspd_relation >= 30 and dnvp_ideology == "Radical") or (uspd_relation >= 40 and dnvp_ideology == "Moderate") and not lvp_formed) or (((lvp_relation >= 30 and dnvp_ideology == "Radical") or (lvp_relation >= 40 and dnvp_ideology == "Moderate")) and lvp_formed)) and (wp_relation >= 40 and lvp_formed) and resources >= 2
subtitle: We will need 2 resources to smooth out the process of forming the coalition. [? if  dnvp_ideology == "Moderate": This will be more difficult due to the <span style="color: #3E88B3;">**DNVP**</span>'s influence on the coalition. ?]
unavailable-subtitle: [? if z_relation < 50 and dnvp_ideology == "Radical" : We do not have adequate relations with the <span style="color: #000000;">Center Party</span>. ?][? if lvp_relation < 40 and lvp_formed and dnvp_ideology == "Moderate": We do not have adequate relations with the <span style="color: #FFCC00;">**LVP**</span>. ?][? if lvp_relation < 30 and lvp_formed and dnvp_ideology == "Radical": We do not have adequate relations with the <span style="color: #FFCC00;">**LVP**</span>. ?][? if uspd_relation < 30 and dnvp_ideology == "Radical" and not lvp_formed: We do not have adequate relations with the <span style="color: #C0A054;">DVP</span>. ?][? if z_relation < 60 and dnvp_ideology == "Moderate" : We do not have adequate relations with the <span style="color: #000000;">Center Party</span>. ?][? if uspd_relation < 40 and dnvp_ideology == "Moderate" and not lvp_formed : We do not have adequate relations with the <span style="color: #C0A054;">DVP</span>. ?] [? if wp_relation < 40 and not lvp_formed: We do not have adequate relations with the <span style="color: #D3C24D;">[+ ddp_name +]</span>. ?][? if resources < 2 : We do not have enough resources. ?]
on-arrival: resources -= 2; spd_in_government = 1; in_grand_coalition = 1; dnvp_toleration = 0; right_dissent += 6; christiansocial_strength += 5; leverage = 20 if leverage >= 20
achievement: constitutional_coalition

Our negotiations have succeeded, and the <span style="color: #000000;">Center Party</span> and [? if not lvp_formed:<span style="color: #C0A054;">**DVP**</span>?][? if lvp_formed:<span style="color: #FFCC00;">**LVP**</span>?] have agreed to form a new coalition government with us!

We are now part of a "constitutional coalition" government that includes all parties except the extremists of the <span style="color: #700000;">**KPD**</span>, <span style="color: #3E88B3;">**DNVP**</span>, and <span style="color: #7A3C00;">**NSDAP**</span>. [+ chancellor +] will remain as chancellor. Leftists in our party are skeptical of this arrangement.

- @election_1928.ministries: Now, we must select our ministries.



@stay_course

We will stay the course of toleration, for the sake of stability.
