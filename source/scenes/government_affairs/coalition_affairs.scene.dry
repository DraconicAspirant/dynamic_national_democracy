title: Coalition Affairs
new-page: true
is-card: true
tags: govt_affairs
on-arrival: month_actions += 1; coalition_affairs_timer = 5
view-if: dnvp_in_government = 1 and coalition_dissent >= 1 and coalition_affairs_timer = 0 and (in_grand_coalition or in_weimar_coalition or in_national_front)
card-image: img/muller_cabinet.jpg
#subtitle: There is dissent within our coalition.

= Coalition Affairs

Our coalition is in trouble. The right-leaning members of the <span style="color: #000000;">Center</span> [? if in_grand_coalition and not lvp_formed: and <span style="color: #C0A054;">**DVP**</span> ?][? if lvp_formed: and <span style="color: #FFCC00;">**LVP**</span> ?] are constantly criticizing us for our policies, and are threatening to call a vote of no confidence to collapse this government[? if spd_prussia :, or to do the same for our government in the state of Prussia?].

How do we reduce tensions with the coalition?

- @dvp_prussia_2: Let the <span style="color: #C0A054;">**DVP**</span> into Prussia.
- @promise_cuts: Agree to the bourgeois plans to cut welfare.
- @resources: Use our party resources to support our allies.
- @bring_down: Instead, we will bring down the government with a no-confidence vote, and call for new elections.
- @no_change: Take none of these actions.
- @easy_discard


@dvp_prussia_2
view-if: year <= 1929 and in_grand_coalition_prussia = 0 and (in_weimar_coalition_prussia or in_minority_conservative_coalition_prussia) and in_grand_coalition = 1 and dvp_was_in_prussia == 0 and not lvp_formed
choose-if: z_leader != "Kaas"
unavailable-subtitle: [? if z_leader = "Kaas": Kaas and the <span style="color: #000000;">Center</span> strongly dissents against. ?]
on-arrival: in_social_coalition_prussia = 0; in_weimar_coalition_prussia = 0; in_grand_coalition_prussia = 1; in_minority_bourgeois_right_coalition_prussia = 0; in_new_grand_coalition_prussia = 0; in_national_front_prussia = 0; in_harzburg_coalition_prussia = 0; in_dnvp_majority_prussia = 0; in_bourgeois_coalition_prussia = 0; in_harzburg_coalition_prussia = 0; in_minority_conservative_coalition_prussia = 0; spd_prussia = 1; ddp_in_national_front_prussia = 0; kpd_goals_seen_prussia = 0; dnvp_toleration_prussia = 0; uspd_relation += 10; z_relation -= 5; wp_relation += 3; coalition_dissent -= 1; right_dissent += 5; in_grand_coalition_prussia = 1; in_weimar_coalition_prussia = 0; in_minority_conservative_coalition_prussia = 0; dvp_left += 3; new_middle_wp += 3;

Letting the <span style="color: #C0A054;">**DVP**</span> into the Prussian government is an offer too good for them to refuse, no matter their leadership. 

However, this has negatively impacted our freedom of action we previously had in the <span style="color: #000000;">We</span><span style="color: #DD0000;">im</span><span style="color: #FFCC00;">ar</span> coalition.

@give_up_prussia
subtitle: Braun would need to resign.
view-if: prussia_leader = "Braun" and neo_weimar_coalition_prussia >= 50
on-arrival: coalition_dissent = 1; prussia_leader = "Stegerwald"; prussia_leader = "Brüning" if z_leader == "Stegerwald"; christiansocial_dissent += 10; agrarian_dissent += 10; youngconservative_dissent += 10; right_dissent += 5; workers_uspd -= 5; rural_uspd -= 5; braun_resign = 1

Fine. We no longer control the government of Prussia. The vote has been called off.

The Minister-President position is transferred to the <span style="color: #000000;">Center</span>.

@promise_cuts
on-arrival: workers_uspd -= 5; unemployed_uspd -= 6; workers_spd += 5; unemployed_spd += 3; unemployed_nsdap += 3; right_dissent += 10; youngconservative_dissent += 5; welfare -= 1; budget += 1; coalition_dissent = 0; dvp_left += 1; ddp_cohesion -= 1; ddp_right += 1; lvp_left += 1; goal_dnvp_cancel += 1; social_welfare_timer = 10

Fine. By cutting welfare, we have reduced dissent within the coalition.

@resources
subtitle: -2 resources
view-if: historical_mode = 0
choose-if: (resources >= 2 and in_weimar_coalition or in_national_front or in_minority_government) or ((resources >= 2 and in_grand_coalition and not (dvp_ideology == "Right" and year >= 1930) and not lvp_formed) or (resources >= 2 and (in_grand_coalition or in_weimar_coalition) and not (lvp_ideology == "Right" and year >= 1930) and lvp_formed))
unavailable-subtitle: [? if resources < 2: We do not have enough resources. ?] [? if in_grand_coalition and (dvp_ideology == "Right" and year >= 1930) and not lvp_formed: The <span style="color: #C0A054;">**DVP**</span> doesn't want our resources anymore. ?] [? if (in_grand_coalition or in_weimar_coalition) and (lvp_ideology == "Right" and year >= 1930) and lvp_formed: The <span style="color: #FFCC00;">**LVP**</span> doesn't want our resources anymore. ?]
on-arrival: resources -= 2; coalition_dissent -= 1

By transferring campaign funds to our coalition partners, we can make our policies more acceptable to them. Some would call this corruption, but perhaps it is necessary to save democracy.

@no_change

No. We will not do this.

@bring_down
subtitle: Bring down this government before our opponents do.
on-arrival: dnvp_in_government = 0; grand_coalition_collapsed = 1 if in_grand_coalition; in_grand_coalition = 0; in_weimar_coalition = 0; chancellor = "Brüning"; chancellor_party = "Z"; next_election_time = time + 3 if next_election_time > time+3; next_election_month = month + 3 if next_election_time >= time+3; next_election_year = year if next_election_time >= time+3; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12; dnvp_toleration = 0; right_strength += 8; anti_democracy -= 5; new_middle_uspd -= 3; rural_uspd -= 3; old_middle_uspd -= 3; catholics_uspd -= 3; christiansocial_dissent += 5; bruning_event_timer = 6 if not bruning_event_seen and year >= 1930
go-to: elections if bruning_event_seen or year < 1930

We have called for a vote of no confidence in ourselves, and have brought down this government. Chancellor [+ chancellor +] currently rules as acting chancellor.

[? if not bruning_event_seen and year >= 1930: New elections haven't been called yet but it's likely that they will be soon, considering Brüning most likely doesn't have the majority of the Reichstag behind him. ?]

@elections
call: set_next_election_time

New elections have been set for [+ next_election_month : month +] [+ next_election_year +].
