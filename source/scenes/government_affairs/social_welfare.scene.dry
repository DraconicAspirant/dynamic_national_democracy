title: Social welfare policy
new-page: true
is-card: true
tags: govt_affairs
on-arrival: social_welfare_timer = 10; month_actions += 1
view-if: spd_in_government = 1 and social_welfare_timer = 0
card-image: img/portraits/JuchaczMarie.jpg

= Social Welfare Policy

The social welfare program, including unemployment insurance, healthcare, and old-age care, is the proudest achievement of the <span style="color: #c00000;">**SPD**</span> in our time in government. There is continuous pressure to dismantle the welfare system[? if black_thursday_seen = 1 :, especially given the economic crisis ?].

- @reduce_spending: Reduce spending on welfare.
- @increase_spending_pre_depression: Increase spending on welfare.
- @increase_spending_post_depression: Increase spending (welfare levels will remain about the same).
- @spending_same_pre_depression: Keep spending the same.
- @spending_same_post_depression: Keep spending the same (effectively a service cut).
- @easy_discard

@reduce_spending
subtitle: This will hurt our proletarian base. +1 Budget
unavailable-subtitle: There is nothing left to cut.
choose-if: welfare >= -5
on-arrival: budget += 1; workers_uspd -= 6; unemployed_uspd -= 6; workers_spd += 3; welfare -= 1; christiansocial_dissent += 10; right_dissent += 10; youngconservative_dissent += 10; agrarian_dissent += 5; paramilitary_dissent += 5; uspd_relation += 5; wp_relation += 2; lvp_relation += 3; z_relation += 3; anti_democracy -= 3; coalition_dissent -= 1 if ((in_grand_coalition or (in_weimar_coalition and lvp_formed)) and coalition_dissent > 0); nsdap_coalition_dissent += 1; nsdap_relation -= 8; inflation -= 0.3; unemployed += 0.4; dvp_left += 1; ddp_right += 1; ddp_cohesion -= 0.5; lvp_left += 0.5; goal_dnvp_cancel += 1; economic_growth -= 0.2;

We have reduced levels of social welfare spending. This means more poverty and suffering for the working class. Our detractors claim that we are sacrificing proletarian livelihoods for the sake of class peace with the bourgeoisie. Are they wrong? Are the workers wrong to turn to the <span style="color: #700000;">**Communists**</span>, in light of our choices?


@increase_spending_pre_depression
view-if: unemployed <= 15
choose-if: budget > 0
subtitle: -1 Budget
on-arrival: budget -= 1; workers_uspd += 6*(1-dissent); unemployed_uspd += 6*(1-dissent); welfare += 1; christiansocial_dissent -= 5; youngconservative_dissent -= 5; agrarian_dissent -= 5; right_dissent -= 5; paramilitary_dissent -= 5; coalition_dissent += 1 if welfare > 1; welfare_goal_completed += 1; welfare_goal_dnvp += 1; inflation += 0.3; unemployed -= 0.4; dvp_right += 1 if welfare > 1; lvp_right += 1 if welfare > 1; ddp_right += 1 if welfare > 2; economic_growth += 0.2;

We have increased welfare spending. Our proletarian base is pleased.

@increase_spending_post_depression
view-if: unemployed > 15
choose-if: budget > 0
on-arrival: budget -= 1; workers_uspd += 3*(1-dissent); unemployed_uspd += 3*(1-dissent); coalition_dissent += 1 if welfare > 0; welfare_goal_completed += 1; welfare_goal_dnvp += 1; inflation += 0.3; dvp_right += 1; ddp_right += 1 if welfare > 1; lvp_right += 1
subtitle: The increased levels of unemployment necessitate more spending on aid to maintain the same benefits. -1 Budget

Increasing welfare spending is necessary in light of the rise in unemployment, so we are essentially treading water.

@spending_same_pre_depression
view-if: unemployed <= 15

We have kept welfare spending the same.


@spending_same_post_depression
view-if: unemployed > 15
on-arrival: workers_uspd -= 3; unemployed_uspd -= 4; welfare -= 1; christiansocial_dissent += 5; right_dissent += 5; youngconservative_dissent += 5; agrarian_dissent += 5; paramilitary_dissent += 5; nsdap_coalition_dissent += 1; nsdap_relation -= 8 if in_national_front or in_harzburg_coalition; goal_dnvp_cancel += 1
subtitle: The increased levels of unemployment necessitate more spending on aid.

Increasing welfare spending is necessary in light of the rise in unemployment, so we have essentially cut welfare. The workers are justifiably displeased - we are sacrificing their livelihoods to appease bourgeois demands for austerity.

[? if in_national_front or in_harzburg_coalition: The <span style="color: #700000;">**KPD**</span> is turning against us for our welfare policy. ?]
